plugins {
   id 'org.springframework.boot' version '2.7.3'
   id 'io.spring.dependency-management' version '1.0.13.RELEASE'
   id 'com.vaadin' version '23.2.0'
   id 'org.flywaydb.flyway' version "9.3.0"
   id 'java'
   id 'idea'
   id 'maven-publish'
   id 'com.bmuschko.docker-spring-boot-application' version '8.1.0'
}

group = 'net.steinerworld'
version = getVersion()
sourceCompatibility = '11'

def getVersion() {
   String buildNr = getBuildNr()
   String branchName = getBranchName()
   String tagName = getTagName()
   String ver = "${MAJOR_VERSION}.${MINOR_VERSION}"
   if (tagName != null && !tagName.equals("null")) {
      ver = tagName
   } else if (buildNr == null || branchName == null) {
      ver += "+local"
   } else if ("master".equals(branchName)) {
      ver += "+" + buildNr
   } else {
      ver += "+" + buildNr + "." + branchName.replace('/', '-')
   }
   println "determined version: $ver"
   return ver;
}

def getBranchName() {
   return project.hasProperty('branchName') ? project.property('branchName') : null
}

def getTagName() {
   return project.hasProperty('tagName') ? project.property('tagName') : null
}

def getBuildNr() {
   return project.hasProperty('buildNr') ? project.property('buildNr') : null
}

configurations {
   compileOnly {
      extendsFrom annotationProcessor
   }
}

repositories {
   mavenCentral()
   maven { url "${vaadinAddonsUrl}" }
}

ext {
   set('vaadinVersion', "23.2.0")
}

dependencies {
   implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
   implementation 'org.springframework.boot:spring-boot-starter-validation'
   implementation 'org.springframework.boot:spring-boot-starter-security'
   implementation 'com.vaadin:vaadin-spring-boot-starter'
   implementation 'com.vaadin:vaadin-core'
   implementation 'org.flywaydb:flyway-core:9.6.0'
   implementation 'com.itextpdf:itextpdf:5.5.13.3'
   implementation 'com.github.appreciated:apexcharts:23.0.0'
   compileOnly 'org.projectlombok:lombok'
   developmentOnly 'org.springframework.boot:spring-boot-devtools'
   runtimeOnly 'org.postgresql:postgresql'
   annotationProcessor 'org.projectlombok:lombok'
   testImplementation 'org.springframework.boot:spring-boot-starter-test'

   // addon
   implementation 'com.vaadin.componentfactory:togglebutton:2.0.0'

}

dependencyManagement {
   imports {
      mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
   }
}

vaadin {
   productionMode = false
}

flyway {
   url = 'jdbc:postgresql://localhost:5435/hypnobook'
   user = 'appuser'
   password = 'appuser'
   locations = ['classpath:db/migration']
}
// we need to build classes before we can migrate
flywayMigrate.dependsOn classes

tasks.named('test') {
   useJUnitPlatform()
}

//bootJar {
//   manifest {
//      attributes(
//            'Main-Class': 'org.springframework.boot.loader.JarLauncher',
//            'Start-Class': 'net.steinerworld.hypnobook.Application',
//            'Spring-Boot-Version': '2.7.3',
//            'Spring-Boot-Classes': 'BOOT-INF/classes/',
//            'Spring-Boot-Lib': 'BOOT-INF/lib/',
//            'Spring-Boot-Classpath-Index': 'BOOT-INF/classpath.idx',
//            'Spring-Boot-Layers-Index': 'BOOT-INF/layers.idx',
//            'Build-date': new Date(),
//            'Name': 'steinerworld/hypnobook',
//            'Implementation-Title': 'Hypno Book',
//            'Implementation-Version': version,
//            'Implementation-Vendor': 'Steinerworld'
//      )
//   }
//}

tasks.create('currentVersion') {
   outputs.file("$buildDir/resources/main/version.txt")
   inputs.property('version', project.version)

   doLast {
      project.file("$buildDir/resources/main/version.txt") << "${project.version};" + new Date()
   }
}

publishing {
   publications {
      maven(MavenPublication) {
         groupId group
         from components.java
         artifact("$buildDir/libs/hypnobook-" + version + ".jar")
      }
   }

   repositories {
      maven {
         url 'https://maven.steinerworld.net/repository/maven-releases/'

         credentials {
            username = System.getenv('REPO_USR')
            password = System.getenv('REPO_PSW')
         }
      }
   }
}

docker {
   springBootApplication {
      baseImage = 'azul/zulu-openjdk-alpine:11-latest'
      ports = [9090, 8080]
      images = ["steinerworld/hypnobook:${version}"]
      jvmArgs = ['-Dspring.profiles.active=production', '-Xmx2048m']
      vaadin {
         productionMode = true
      }
      registryCredentials {
         url = '${dockerHubRepository}'
         username = System.getenv('DOCKER_USR')
         password = System.getenv('DOCKER_PSW')
         email = 'michael@steinerworld.net'
      }
   }
}
